plugins {
    id 'application'
    id 'java'
}

group = 'com.pathfinder'
version = '1.0'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

// Detect current OS for JavaFX classifiers
def os = org.gradle.internal.os.OperatingSystem.current()
def platform
if (os.isWindows()) {
    platform = 'win'
} else if (os.isMacOsX()) {
    platform = 'mac'
} else if (os.isLinux()) {
    platform = 'linux'
} else {
    throw new GradleException("Unsupported OS")
}

def javafxVersion = '17.0.2'

dependencies {
    implementation "com.gluonhq:maps:2.0.0-ea+6"

    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-web:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-media:${javafxVersion}:${platform}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
}

application {
    mainClass = 'com.pathfinder.Main'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
        '--module-path', configurations.runtimeClasspath.asPath,
        '--add-modules', 'javafx.controls,javafx.web,javafx.fxml,javafx.media'
    ]
}

run {
    jvmArgs = [
        '--module-path', configurations.runtimeClasspath.asPath,
        '--add-modules', 'javafx.controls,javafx.web,javafx.fxml,javafx.media'
    ]
}
